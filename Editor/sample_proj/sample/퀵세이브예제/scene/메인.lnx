#메인.lnx

@매크로 퀵세이브:
	pass
	:퀵세이브
	[저장 번호=101 저장제목="퀵세이브" ] #퀵세이브는 다른 저장과는 다른 번호를 사용합니다.
	
	@조건 $불러오기 == 1:
		# 막 불러온 경우
		[텍스트 내용="퀵로드에 성공하였습니다." 아이디="퀵성공"  x=650 y=300 투명도=0]
		[사용자애니메이션 노드1="퀵성공" 이름="퀵세이브로드애니메이션"  ]
	@그외 : 
		# 저장하는 경우
		[텍스트 내용="퀵세이브에 성공하였습니다." 아이디="퀵성공"  x=650 y=300 투명도=0  스크린샷노출="아니오"]
		[사용자애니메이션 노드1="퀵성공" 이름="퀵세이브로드애니메이션"  ]

	$불러오기=0

@매크로 퀵로드:
	pass
	:퀵로드
	[저장체크 번호=101  ]
	@조건 저장체크.결과:
		$불러오기=1
		[불러오기 번호=101  ] #퀵세이브에서 사용한 번호로 불러오기를 합니다.
	@그외 : 
		[텍스트 내용="퀵세이브 내역이 없습니다." 아이디="퀵실패"  x=650 y=300 투명도=0]
		[사용자애니메이션 노드1="퀵실패" 이름="퀵세이브로드애니메이션"  ]

@애니메이션 퀵세이브로드애니메이션 :
	@노드 1:
		@프레임 0:
			&색상A 0
		@프레임 3:
			&색상A 255
		@프레임 15:
			&색상A 255
		@프레임 20:
			&색상A 0

[대사창수정 아이디="대화" 폰트크기="30"  ]
[이미지 아이디="퀵세이브" 파일명="menuButtons_save.png"   북마크이동="퀵세이브"  x=700 y=560  GUI="예"]
[이미지 아이디="퀵로드" 파일명="menuButtons_load.png"   북마크이동="퀵로드"  x=500 y=560  GUI="예"]
[이미지 아이디="배경" 파일명="Camera_025_ReversSunset_filtered.png"  ]
$불러오기=0

:반복점
[이미지 아이디="피니" 파일명="피니_노멀.png" ]
[대화 이름="피니"  ]
;안녕! 난 피니야!
;이 예제에서는 퀵세이브에 대해서 설명할 거야!

[대화 이름="피니"  ]
;퀵세이브의 원리는 보통의 저장과 불러오기와 원리가 같아!
;단지 보통의 저장과 불러오기와는 달리 <색상 0 255 255 >혼자만의 저장할 번호</색상>만 부여하면 돼!

[이미지 아이디="피니" 파일명="피니_웃음.png" ]
[대화 이름="피니"  ]
;이번 예제에서는 퀵세이브에 <색상 0 255 255 >101</색상>번을 사용해! 보통의 저장하기도 같이 구현한다면, 그 저장하기 슬롯에는 101번만 피해가면 되겠지?

[대화 이름="피니"  ]
;반드시 이번 예제처럼 <색상 0 255 255 >101</색상>번을 사용할 필요는 없어! 단지 퀵세이브 전용의 저장 번호만 있으면 돼!
;물론, 이것을 응용해서 여러개의 퀵 슬롯을 운용하는 것도 가능하다구!

[이미지 아이디="피니" 파일명="피니_의문.png" ]
[대화 이름="피니"  ]
;퀵세이브에 대한 설명은 이상이야! 이미 보고 있겠지만, 아래의 불러오기와 저장하기 버튼을 눌러보라구!

[대화 이름="피니"  ]
;아참, 원활한 테스트를 위해 이 예제는 반복되도록 해놨으니까, 다시 설명한다고 당황하지 말라구!
;

[대화 이름=""  ]
;

>반복점