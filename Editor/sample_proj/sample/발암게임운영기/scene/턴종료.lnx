$턴종료횟수 = $턴종료횟수 + 1

[대사창수정 아이디="보고서독백" 이미지="노트.png" 위치="0,960" 여백="50,200" 색상="255,255,255,255" 폰트크기=27.9 폰트그림자="0,0,0,255,1,1" 폰트색상="0,0,0,255"]
[화면초기화]


#남은 컨텐츠와 버그에 따른 잔존률 계산
@조건 $남은컨텐츠 > 0:
	$남은컨텐츠 = $남은컨텐츠 - ($유저수 / 20000)

@조건 $남은컨텐츠 < 0:
	$남은컨텐츠 = 0

$남은컨텐츠 = [소수점버림 수=$남은컨텐츠]

@조건 $남은컨텐츠 < 10:
	$잔존률 = $잔존률 - ($버그*0.03) - (10-$남은컨텐츠)*0.03
@그외:
	$잔존률 = 0.9 - ($버그*0.03)

@조건 $잔존률 < 0 :
	$잔존률 = 0.1


유저획득인지도 = $인지도
@조건 유저획득인지도 < 0:
	유저획득인지도 = 0
#유저 획득량 계산
유저획득량 = [랜덤 끝=(유저획득인지도 * 5) 시작=(유저획득인지도 * 2) ]
유저획득량 = [소수점버림 수=유저획득량 ]


#유저이탈량 계산
유저이탈량 = ($유저수-($유저수 * $잔존률))
유저이탈량 = 유저이탈량 * (1-($운영자 * 0.01))
유저이탈량 = [소수점버림 수=유저이탈량 ]


유저수변화량 =  유저획득량 - 유저이탈량
유저수변화량 = [소수점버림 수=유저수변화량 ]
기존유저수 = $유저수
$유저수 = $유저수 + 유저수변화량


월급 = ($프로그래머 + $그래픽 + $기획자 + $운영자 + $QA) * 200
서버비용 = $서버 * $서버대당유지비
매출 = 기존유저수 * $객단가
매출 = [소수점버림 수=매출 ]
수익 = 매출-(월급+서버비용)
$자금 = $자금 + 수익

[로그 텍스트="인지도:" + $인지도 ]
[로그 텍스트="남은컨텐츠:" + $남은컨텐츠 ]
[로그 텍스트="잔존률:" + $잔존률 ]
[로그 텍스트="애사심:"+$애사심 ]


$애사심 = ($애사심 * 0.7) + ($남은시간 * 0.1)
$인지도 = $인지도 * 0.3

	
$시스템업데이트개발량 = $시스템업데이트개발량+($프로그래머*0.3)+($기획자*0.2)+($그래픽*0.2)+($사운드*0.1)
$컨텐츠업데이트개발량 = $컨텐츠업데이트개발량+($그래픽*0.3)+($기획자*0.2)+($사운드*0.3)

[독백 대사창="보고서독백"]
;<색상 0 0 255><=$게임명>  <= $턴종료횟수 > 개월차 보고서</색상>
;
;매출 : <=매출> 만원
;월급 : <=월급> 만원
;서버 유지비용 : <=서버비용> 만원
;총 수익 : <=수익> 만원
;
;신규유저 : <=유저획득량> 명
;이탈유저 : <=유저이탈량> 명
;유저수 변화량 : <=유저수변화량> 명
;


[스크립트 실행="예" 파일명="이벤트_없데이트.lnx" ]
[스크립트 실행="예" 파일명="이벤트_버그발견.lnx" ]
[스크립트 실행="예" 파일명="이벤트_직원퇴사.lnx"]
[스크립트 실행="예" 파일명="이벤트_서버터짐.lnx" ]
[스크립트 실행="예" 파일명="이벤트_노무분쟁.lnx" ]
[스크립트 실행="예" 파일명="게임오버.lnx" ]

$남은시간 = 160

>>주메뉴시작